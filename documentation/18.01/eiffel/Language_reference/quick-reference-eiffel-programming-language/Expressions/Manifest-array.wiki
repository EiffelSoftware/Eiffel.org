[[Property:uuid|3C1A6DEF-A6F1-4E64-A0BE-C07BDB382C93]]
[[Property:weight|0]]
[[Property:title|Manifest array]]

A manifest array is an expression denoting an array by simply listing its elements, as in  <code><<1, 4, 9, 16, 25>></code>. The lower index is always <code>1</code> and the upper index is the number of items,  <code>5</code> in this example.

The type of a manifest array is always  <code>ARRAY [T]</code> where  <code>T</code> is a type to which all the elements conform, <code>INTEGER</code> in the previous example. In case of a possible ambiguity you can make the type explicit, as in <code>{ARRAY [COMPARABLE]} <<7, "Eiffel">></code>, where both  <code>INTEGER</code>, the type of <code>7</code>, and  <code>STRING</code>, the type of <code>"Eiffel"</code>, conform to  <code>COMPARABLE</code>. 

== What are manifest arrays good for? ==

Use a manifest array to initialize an element by simply listing its initial elements. For example, with the declaration

```eiffel
  squares: ARRAY [INTEGER]
```

you can initialize <code>squares</code> through

```eiffel
  squares := <<1, 4, 9, 16, 25>>
```

This is simpler than the alternative, which would be to create the array explicitly and give a value to every element in turn:
```eiffel
   create squares .make_filled (0, 1, 5)
			-- Arguments to `make_filled' are: default value, lower bound, upper bound.
   squares  [1] := 1 ; squares  [2] := 4 ; squares  [3] := 9 
   squares  [4] := 16 ; squares  [5] := 25
```

The first form, with the manifest array, is shorter, but the effect is the same.

Manifest arrays are normal arrays, not restricted in any way. You can for example add elements to them, as in

```eiffel
   squares.force (36, 6)	
			-- Arguments to `force' are: value, position.
```

which will resize the array to bounds 1 and 6.

== Type of a manifest array: the implicit case ==

If you do not explicitly specify an array type, the type of the manifest array is as follows:

* For an empty manifest array  <code><<>></code>:  <code>ARRAY [NONE]</code>. (In the following cases we assume the array is not empty.)

* If all elements are of the same exact type  <code>T</code>:  <code>ARRAY [T]</code>.

* If the types of all elements all conform to a type  <code>T</code>:  <code>ARRAY [T]</code>. Note that in this case  <code>T is unique since two different types cannot conform to each other. (The preceding case, all types identical, is a special case of this one, since a type conforms to itself.) 

* Otherwise:  <code>ARRAY [ANY]</code>.

As an example of the third case (conformance of all elements to one of them), assume  <code>POLYGON</code> and  <code>CIRCLE</code> both conform to  <code>FIGURE</code>. Then the manifest array  <code><<a_polygon, a_figure, a_circle>></code>, with  <code>a_polygon of type  <code>POLYGON</code> and so on, is  <code>ARRAY [FIGURE]</code>.

== Type of a manifest array: the explicit case ==

With the preceding rule, the type of   <code><<7, "Eiffel">></code> is the most general possible one, <code>ARRAY [ANY]</code>, since  <code>INTEGER</code> and  <code>STRING</code> do not conform to each other (either way). If you are not happy with this default type, you can make the array type explicit by writing it in braces:

```eiffel
{ARRAY [COMPARABLE]} <<7, "Eiffel">>
```

The rule in such a case is that in <code>{ARRAY [T]} <<element1, element2, ...>></code> the types of all elements must conform to  <code>T</code>.

As another example, with

```eiffel
figures: ARRAY [FIGURE]
```

you cannot assign  <code><<a_polygon, a_circle>></code> to <code>figures</code> since the type of the manifest array is <code>ARRAY [ANY]</code>. To make this assignment possible, use an explicit type:

```eiffel
figures := {ARRAY [FIGURE]} <<a_polygon, a_circle>>
```

You can also use this form to give an explicit type to an empty array, which would otherwise be of type <code>ARRAY [NONE]</code>. For example, with <code>figures</code> declared as above:

```eiffel
figures := {ARRAY [FIGURE]} <<>>
```
